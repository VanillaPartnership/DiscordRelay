/*
 * discord-relay: A Minecraft <=> Discord relay, but for Vanilla servers.
 * Copyright (C) 2018 VanillaPartnership
 *
 * This program is free software: you can redistribute it and/or modify
 * it under the terms of the GNU General Public License as published by
 * the Free Software Foundation, either version 3 of the License, or
 * (at your option) any later version.
 *
 * This program is distributed in the hope that it will be useful,
 * but WITHOUT ANY WARRANTY; without even the implied warranty of
 * MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
 * GNU General Public License for more details.
 *
 * You should have received a copy of the GNU General Public License
 * along with this program.  If not, see <https://www.gnu.org/licenses/>.
 */

plugins {
    id 'io.franzbecker.gradle-lombok' version '1.14'
    id 'com.github.johnrengelman.shadow' version '4.0.3'
    id 'java'
    id 'idea'
    id 'maven'
}

group 'net.valkyrin'
version '2.0-RC1'

lombok {
    version = '1.18.0'
    sha256 = ""
}

defaultTasks 'clean', 'javadoc', 'check', 'build'

sourceCompatibility = 1.8
targetCompatibility = 1.8


repositories {
    jcenter()
    mavenCentral()
}

javadoc {
    options.memberLevel = JavadocMemberLevel.PUBLIC
    options.author()
    options.encoding = 'UTF-8'
    options.addBooleanOption('html5', true)
    options.addBooleanOption('Xdoclint:none', true)
    failOnError = false
}

task sourcesJar(type: Jar, dependsOn: classes) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from javadoc.destinationDir
}

artifacts {
    archives sourcesJar
    archives javadocJar
}

test {
    reports {
        junitXml.enabled = false
        html.enabled = true
    }
    ignoreFailures = true
    subprojects.each { dependsOn("${it.path}:test") }
}

task wrapper(type: Wrapper) {
    gradleVersion = '4.8.1'
    //noinspection UnnecessaryQualifiedReference
    distributionType = Wrapper.DistributionType.ALL
}

jar {
    manifest {
        attributes(
                "Implementation-Title": name,
                "Implementation-Version": version,
                "Main-Class": "net.valkyrin.relay.Main"
        )
    }
}

dependencies {
    compile group: 'clj-rcon', name: 'clj-rcon', version: '0.1.0'
    compile group: 'com.google.guava', name: 'guava', version: '27.0.1-jre'
    compile(group: 'net.dv8tion', name: 'JDA', version: '3.8.1_450') {
        exclude group: 'club.minnced', module: 'opus-java'
    }
    // compile group: 'club.minnced', name: 'discord-webhooks', version: '0.1.4'
    compile group: 'net.kyori', name: 'text', version: '1.12-1.6.4'
    testCompile group: 'junit', name: 'junit', version: '4.12'
    testCompile group: 'org.slf4j', name: 'slf4j-simple', version: '1.7.25'
}
